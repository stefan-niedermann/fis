plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.6.4'
    id 'org.openapi.generator' version '5.4.0'
    id 'java'
}

group = 'it.niedermann'
version = '2.2.6-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // OpenAPI
    implementation 'org.openapitools:openapi-generator-gradle-plugin:5.4.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.2'

    // OCR
    implementation 'net.sourceforge.tess4j:tess4j:5.2.0'

    // FTP
    implementation 'commons-net:commons-net:3.8.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Mail address validation
    implementation 'commons-validator:commons-validator:1.7'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-inline:4.4.0' // Needed for mocking records https://stackoverflow.com/a/14292888
}

configurations.all {
    exclude module: 'slf4j-simple'
}

bootJar {
    launchScript()
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/api.yml".toString()
    outputDir = "$buildDir/gen"
    apiPackage = "it.niedermann.fis.main.api"
    invokerPackage = "it.niedermann.fis.main.invoker"
    modelPackage = "it.niedermann.fis.main.model"
    modelNameSuffix = "Dto"
    configOptions = [
        interfaceOnly: "true",
        dateLibrary: "java11",
        delegatePattern: "true",
        useSwaggerUI: "false",
        useSpringfox: "false"
    ]

    jar {
        enabled = false
    }
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/gen/src/main/java")
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate